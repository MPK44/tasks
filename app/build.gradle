apply plugin: 'com.android.application'

task wrapper(type: Wrapper) {
    gradleVersion = '4.9'
}

buildscript {
    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.0-alpha07'
    }
}

repositories {
    jcenter()
    google()
}

android {
    lintOptions {
        lintConfig file('lint.xml')
        textOutput 'stdout'
        textReport true
        abortOnError false
    }

    compileSdkVersion 27
    buildToolsVersion '28.0.2'

    defaultConfig {
        applicationId 'com.bmaehr.tasks'
        versionCode 458
        versionName '4.9.15'
        targetSdkVersion 27
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        release
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            multiDexEnabled true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions 'store', 'env'

    productFlavors {
        generic {
            dimension 'store'
        }
        googleplay {
            dimension 'store'
        }
        amazon {
            dimension 'store'
        }
        dev {
            minSdkVersion 27
            dimension 'env'
        }
        prod {
            minSdkVersion 15
            dimension 'env'
        }
    }

    if (project.hasProperty('keyAlias') &&
            project.hasProperty('storeFile') &&
            project.hasProperty('storePassword') &&
            project.hasProperty('keyPassword')) {
        android.signingConfigs.release.keyAlias = keyAlias
        android.signingConfigs.release.storeFile = file(storeFile)
        android.signingConfigs.release.storePassword = storePassword
        android.signingConfigs.release.keyPassword = keyPassword
    } else {
        buildTypes.release.signingConfig = null
    }
}

configurations {
    all*.exclude group: 'com.google.guava', module: 'guava-jdk5'
    all*.exclude group: 'org.apache.httpcomponents', module: 'httpclient'
}

final DAGGER_VERSION = '2.9'
final BUTTERKNIFE_VERSION = '8.6.0'
final GPS_VERSION = '15.0.1'
final SUPPORT_VERSION = '27.1.1'
final SUPPORT_ANNOTATIONS_VERSION = '27.0.1'
final STETHO_VERSION = '1.4.2'
final TESTING_SUPPORT_VERSION = '0.5'

dependencies {
    annotationProcessor "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"
    implementation "com.google.dagger:dagger:${DAGGER_VERSION}"

    annotationProcessor "com.jakewharton:butterknife-compiler:${BUTTERKNIFE_VERSION}"
    implementation "com.jakewharton:butterknife:${BUTTERKNIFE_VERSION}"

    debugImplementation ("com.facebook.stetho:stetho:${STETHO_VERSION}") {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }
    debugImplementation "com.facebook.stetho:stetho-timber:${STETHO_VERSION}@aar"
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5'
    //noinspection GradleCompatible
    debugImplementation 'com.android.support:multidex:1.0.3'

    implementation 'com.github.rey5137:material:1.2.4'
    implementation 'com.nononsenseapps:filepicker:4.0.0'
    implementation "com.android.support:design:${SUPPORT_VERSION}"
    implementation "com.android.support:support-annotations:${SUPPORT_ANNOTATIONS_VERSION}"
    implementation "com.android.support:support-v13:${SUPPORT_VERSION}"
    implementation "com.android.support:cardview-v7:${SUPPORT_VERSION}"
    implementation 'com.jakewharton.timber:timber:4.5.1'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    //noinspection GradleDependency
    implementation 'com.google.guava:guava:20.0'
    implementation 'com.jakewharton:process-phoenix:1.1.1'
    implementation 'com.google.android.apps.dashclock:dashclock-api:2.0.0'
    implementation 'com.twofortyfouram:android-plugin-api-for-locale:1.0.2'
    implementation 'com.bignerdranch.android:recyclerview-multiselect:0.2'
    implementation ('com.rubiconproject.oss:jchronic:0.2.6') {
        transitive = false
    }
    implementation ('org.scala-saddle:google-rfc-2445:20110304') {
        transitive = false
    }
    implementation ('com.wdullaer:materialdatetimepicker:3.1.3') {
        exclude group: 'com.android.support', module: 'support-v4'
    }

    googleplayImplementation "com.google.android.gms:play-services-location:${GPS_VERSION}"
    googleplayImplementation "com.google.android.gms:play-services-analytics:16.0.3" // ${GPS_VERSION}
    googleplayImplementation "com.google.android.gms:play-services-auth:${GPS_VERSION}"
    googleplayImplementation "com.google.android.gms:play-services-places:${GPS_VERSION}"
    googleplayImplementation 'com.google.apis:google-api-services-tasks:v1-rev46-1.22.0'
    googleplayImplementation 'com.google.api-client:google-api-client-android:1.22.0'
    googleplayImplementation 'com.google.code.gson:gson:2.8.1'

    amazonImplementation "com.google.android.gms:play-services-analytics:16.0.3" // ${GPS_VERSION}

    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"
    androidTestAnnotationProcessor "com.jakewharton:butterknife-compiler:${BUTTERKNIFE_VERSION}"
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestImplementation 'com.natpryce:make-it-easy:4.0.1'
    androidTestImplementation "com.android.support.test:runner:${TESTING_SUPPORT_VERSION}"
    androidTestImplementation "com.android.support.test:rules:${TESTING_SUPPORT_VERSION}"
    androidTestImplementation "com.android.support:support-annotations:${SUPPORT_ANNOTATIONS_VERSION}"
}
